#!/usr/bin/env coffee

{execSync} = require('child_process')
argv = require('yargs').argv
require 'colors'

$cordova = './node_modules/.bin/cordova'

run = ->
    [task, config] = argv._
    config ?= 'local'
    func = $[task]

    if func
        func config
    else
        console.log "Couldn\'t find task `#{task}`"

sh = (cmd, arg2, arg3) ->

    if arg3 or (typeof arg2 is 'string')
        msg = arg2
        options = arg3
    else if typeof arg2 is 'object'
        options = arg2

    msg ?= cmd

    options ?= {failOK: false}
    print msg, 'blue'

    try
        execSync cmd
    catch e
        if not options.failOK
            print "Error running command '#{cmd}'", 'red'
            console.log e.stack
            print "Aborting.", 'red'
            process.exit()

gulp = (task, config) ->
    cmd = "./node_modules/.bin/gulp #{task}"
    cmd += " --config=#{config}"  if config
    sh cmd, 'Building with Gulp'

installPlugins = ->
    plugins = [
        'cordova-plugin-console'
        'cordova-plugin-dialogs'
        'cordova-plugin-statusbar'
        'cordova-plugin-geolocation'
        'cordova-plugin-file-transfer'
        'cordova-plugin-device',
        # 'org.apache.cordova.globalization'
        # Need to sort this one out 'de.appplant.cordova.plugin.badge@0.6.1'
        # Old version, incompatible with latest cordova 'https://github.com/Shifts/cordova-plugin-inappbrowser.git'
        'cordova-plugin-inappbrowser'
        # 'https://github.com/NorthMcCormick/cordova-plugin-local-notifications'
        'https://github.com/EddyVerbruggen/cordova-plugin-actionsheet.git'
        'https://github.com/whiteoctober/cordova-plugin-app-version.git'
        'https://github.com/apache/cordova-plugin-contacts.git'
        # Not being used right now 'https://github.com/phonegap-build/PushPlugin.git'
    ]

    for plugin in plugins
        sh "#{$cordova} plugin add #{plugin}"

print = (msg, color='cyan') ->
    console.log '[builder] '.gray + msg[color]

cleanPlatforms = (platform) ->
    sh "./platforms/#{platform}/cordova/clean", "Cleaning old #{platform} builds", {failOK: true}

buildToDevice = (config, platform, runOnly) ->

    cleanPlatforms platform

    unless runOnly
        gulp 'build', config

    sh "#{$cordova} run #{platform} --device --justlaunch", "Compiling to #{platform} device"

###
## Define tasks below!
###

$ =
    setup: (config) ->
        sh 'rm -rv configs/local.cson platforms/ plugins/', {failOK: true}
        sh 'npm install'
        sh 'mkdir platforms'
        sh 'echo "{build: useLiveReload: true }" > configs/local.cson'
        sh "#{$cordova} platform add ios"
        gulp 'build', config
        installPlugins()

    plugins: ->
        installPlugins()

    emulate: (config) ->
        cleanPlatforms 'ios'
        gulp 'build', config
        sh "#{$cordova} emulate ios",  "Compiling to iOS device"

        print 'Done!', 'green'

    run: (config) ->
        buildToDevice config, 'ios'
        print 'Done!', 'green'

    runAll: (config) ->
        buildToDevice config, 'ios', true
        print 'Done!', 'green'

    createIcons: ->
        sh "./ios-icon-generator.sh ../../buildAssets/prod-icon/iTunesArtwork@2x.png ../../buildAssets/prod-icon/"

    upload: (config) ->
        print 'Using config ' + config, 'magenta'
        gulp 'build', config
        gulp '_upload', config
        print 'Done!', 'green'

    appStore: (config) ->
        if config isnt 'local'
            print 'Prod config is forced for App Store build. Run again without a config', 'red'
            process.exit()

        $.setup 'prod'
        gulp 'build', 'prod'
        sh "#{$cordova} build --release ios", "Compiling to iOS for release"

        console.log ''
        # print 'Installing Crashlytics'
        # sh './crashlytics.sh'
        # sh 'ruby crashlytics.rb'
        # sh 'xcproj -p platforms/ios/Robby.xcodeproj touch'

        sh 'open platforms/ios'

        print 'Done.', 'green'

run()
